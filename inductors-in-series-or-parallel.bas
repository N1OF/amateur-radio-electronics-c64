100 REM THIS IS PROGRAM NO. 26 PROG26
110 REM THIS PROGRAM WILL CALCULATE
115 REM THE VALUE OF N INDUCTORS IN
120 REM EITHER SERIES OR PARALLEL
125 REM ASSUMING NO MUTUAL INDUCTANCE
130 DIM L(1000)
140 DIM G(1000)
150 GSUM = 0
160 RSUM = 0
170 GOSUB 610
180 PRINT "THIS PROGRAM WILL CALCULATE THE SERIES"
190 PRINT "OR PARALLEL INDUCTANCE OF A"
195 PRINT "NETWORK, PROVIDED THAT"
200 PRINT "THERE IS ZERO MUTUAL INDUCTANCE"
210 PRINT
220 PRINT "ALL INDUCTANCE VALUES SHOULD BE ENTERED IN SAME UNITS, E.G."
225 PRINT "MICROHENRYS (UH)"
230 PRINT
240 PRINT "HOW MANY INDUCTORS ARE THERE?"
250 INPUT N
260 GOSUB 610
270 FOR I = 1 TO N
280 L(I) = 0
290 NEXT I
300 FOR I = 1 TO N
310 PRINT "ENTER L(";I;"):"
320 INPUT L(I)
330 G(I) = 1/L(I)
340 GSUM = GSUM + G(I)
350 RSUM = RSUM + L(I)
360 NEXT I
370 GOSUB 610
380 RTP = 1/GSUM
390 PRINT "PARALLEL OR SERIES?"
400 PRINT TAB(5);"1. PARALLEL"
410 PRINT TAB(5);"2. SERIES"
420 PRINT TAB(5);"3. BOTH PARALLEL AND SERIES"
430 PRINT
440 PRINT "SELECT ONE FROM ABOVE AND PRESS CR..."
450 INPUT D
460 IF D > 3 THEN GOTO 390
465 IF D < 1 THEN GOTO 390
470 ON D GOTO 530,500,560
500 GOSUB 610
510 PRINT "SERIES TOTAL OF ";N;" INDUCTORS IS: ";RSUM;" UH"
520 GOTO 650
530 GOSUB 610
540 PRINT "PARALLEL TOTAL OF ";N;" INDUCTORS IS: ";RTP;" UH"
550 GOTO 650
560 GOSUB 610
570 PRINT "TOTAL PARALLEL INDUCTANCE: ";RTP;" UH"
580 PRINT
590 PRINT "TOTAL SERIES INDUCTANCE: ";RSUM;" UH"
600 GOTO 650
610 FOR I = 1 TO 30
620 PRINT
630 NEXT I
640 RETURN
650 PRINT
660 PRINT "PRESS CR TO CONTINUE..."
670 INPUT KK
680 GOSUB 610
690 PRINT "FINISHED?"
700 PRINT TAB(5);"1. YES"
710 PRINT TAB(5);"2. NO"
720 PRINT
730 PRINT "SELECT ONE FROM ABOVE AND PRESS CR..."
740 INPUT F
750 IF F > 2 THEN GOTO 690
755 IF F < 1 THEN GOTO 690
760 IF F = 2 THEN GOTO 150
770 GOSUB 610
780 PRINT "PROGRAM ENDED"
790 PRINT
800 PRINT
810 END
